def pomConfig = {
  resolveStrategy = Closure.DELEGATE_FIRST
  name project.name
  description project.description
  url "https://github.com/jaredsburrows/retrofit2-synchronous-adapter"

  issueManagement {
    system "github"
    url "https://github.com/jaredsburrows/retrofit2-synchronous-adapter/issues"
  }

  scm {
    url "https://github.com/jaredsburrows/retrofit2-synchronous-adapter"
    connection "scm:https://github.com/jaredsburrows/retrofit2-synchronous-adapter"
    developerConnection "scm:git@github.com:jaredsburrows/retrofit2-synchronous-adapter.git"
  }

  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }

  developers {
    developer {
      id "jaredsburrows"
      name "Jared Burrows"
      email "jaredsburrows@gmail.com"
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  group = "Publications"
  description = "Create jar of the sources."
  classifier = "sources"
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  group = "Publications"
  description = "Create javadoc jar of Java documentation."
  classifier = "javadoc"
  from javadoc.destinationDir
}

task testsJar(type: Jar) {
  group = "Publications"
  description = "Create jar of all tests."
  classifier = "tests"
  from sourceSets.test.output
}

task reportsZip(type: Zip, dependsOn: check) {
  group = "Publications"
  description = "Create a zip of all reports."
  classifier = "reports"
  from reporting.baseDir
}

// Local published to ~/.m2 - mavenLocal()
publishing {
  repositories {
    mavenLocal()
  }

  publications {
    maven(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      artifact testsJar
      artifact reportsZip

      groupId project.group
      artifactId project.name
      version project.version

      pom {
        withXml {
          // All dependencies should use the default scope "compile" not "runtime"
          asNode().dependencies.dependency.each { dep ->
            if (dep.scope.text() == "runtime") dep.remove(dep.scope)
          }

          // Add parent POM information
          asNode().children().last() + pomConfig
        }
      }
    }
  }
}
publish.dependsOn jar, sourcesJar, javadocJar, testsJar, reportsZip
publish.dependsOn "generatePomFileForMavenPublication"

// Snapshots published to JFrog Artifactory repository
artifactory {
  contextUrl = "https://oss.jfrog.org"

  publish {
    repository {
      repoKey = "oss-snapshot-local"
      username = project.properties["BINTRAY_USERNAME"] ?: System.getenv("BINTRAY_USERNAME")
      password = project.properties["BINTRAY_API_KEY"] ?: System.getenv("BINTRAY_API_KEY")
    }

    defaults {
      publications "maven"
    }
  }

  resolve {
    repository {
      repoKey = "libs-release"
    }
  }
}
artifactoryPublish.dependsOn jar, sourcesJar, javadocJar, testsJar, reportsZip
artifactoryPublish.dependsOn "generatePomFileForMavenPublication"

// Releases published to JFrog Bintray's JCenter repository
bintray {
  user = project.properties["BINTRAY_USERNAME"] ?: System.getenv("BINTRAY_USERNAME")
  key = project.properties["BINTRAY_API_KEY"] ?: System.getenv("BINTRAY_API_KEY")
  publications = ["maven"]
  publish = true

  pkg {
    repo = "maven"
    name = project.name
    desc = project.description
    websiteUrl = "https://github.com/jaredsburrows/retrofit2-synchronous-adapter"
    issueTrackerUrl = "https://github.com/jaredsburrows/retrofit2-synchronous-adapter/issues"
    vcsUrl = "https://github.com/jaredsburrows/retrofit2-synchronous-adapter.git"
    licenses = ["Apache-2.0"]
    labels = ["retrofit", "retrofit2", "synchronous", "adapter"]
    githubRepo = "jaredsburrows/retrofit2-synchronous-adapter"
    githubReleaseNotesFile = "README.md"

    version {
      name = project.version
      desc = project.description
      released = new Date()
      vcsTag = project.version
      gpg {
        sign = true
        passphrase = project.properties["SIGNING_PASSWORD"] ?: System.getenv("SIGNING_PASSWORD")
      }
      mavenCentralSync {
        sync = false
        user = project.properties["NEXUS_USERNAME"] ?: System.getenv("NEXUS_USERNAME")
        password = project.properties["NEXUS_PASSWORD"] ?: System.getenv("NEXUS_PASSWORD")
        close = "1"
      }
    }
  }
}
bintrayUpload.dependsOn jar, sourcesJar, javadocJar, testsJar, reportsZip
bintrayUpload.dependsOn "generatePomFileForMavenPublication"

task release(dependsOn: bintrayUpload) {
  group = "Publishing"
  description = "Publish artifact to JFrog Jcenter's Brintray."
}
