plugins {
  id 'com.gradle.build-scan' version '1.16'
  id 'com.jfrog.artifactory' version '4.8.1'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'com.github.ben-manes.versions' version '0.20.0'
  id 'net.ltgt.errorprone' version '0.6'
  id 'java-library'
  id 'maven-publish'
}

repositories {
  jcenter()
}

apply from: 'gradle/dependencies.gradle'

group = GROUP
version = VERSION_NAME
description = POM_DESCRIPTION

sourceCompatibility = versions.java
targetCompatibility = versions.java

jar {
  manifest {
    attributes(
      'Implementation-Title': POM_NAME,
      'Implementation-Version': version,
      'Built-By': System.getProperty('user.name'),
      'Built-Date': new Date(),
      'Built-JDK': System.getProperty('java.version'),
      'Built-Gradle': gradle.gradleVersion)
  }
}

dependencies {
  implementation deps.squareup.retrofit2.retrofit

  compileOnly deps.jsr305

  testImplementation deps.junit
  testImplementation deps.assertjCore
  testImplementation deps.mockitoInline
  testImplementation deps.squareup.okhttp3.mockwebserver
  testImplementation deps.squareup.retrofit2.gson
  testImplementation deps.jsr305
}

apply from: 'gradle/scan.gradle'
apply from: 'gradle/compile.gradle'
apply from: 'gradle/publish.gradle'
