plugins {
  id 'com.github.ben-manes.versions' version '0.42.0'
  id 'java-library'
  id 'groovy'
  id 'maven-publish'
  id 'signing'
}

repositories {
  mavenCentral()
}

apply from: 'gradle/dependencies.gradle'

group = GROUP
version = VERSION_NAME
description = POM_DESCRIPTION

sourceCompatibility = versions.java
targetCompatibility = versions.java

jar {
  manifest {
    attributes(
      'Implementation-Title': POM_NAME,
      'Implementation-Version': archiveVersion,
      'Built-By': System.getProperty('user.name'),
      'Built-Date': new Date(),
      'Built-JDK': System.getProperty('java.version'),
      'Built-Gradle': gradle.gradleVersion)
  }
}

dependencies {
  implementation deps.squareup.retrofit2.retrofit

  compileOnly deps.jsr305

  testImplementation deps.junit
  testImplementation deps.assertjCore
  testImplementation deps.mockitoInline
  testImplementation deps.squareup.okhttp3.mockwebserver
  testImplementation deps.squareup.retrofit2.gson
  testImplementation deps.jsr305
}

apply from: 'gradle/publish.gradle'

tasks.withType(JavaCompile) {
  sourceCompatibility = rootProject.versions.java
  targetCompatibility = rootProject.versions.java

  configure(options) {
    compilerArgs << '-Xlint:all'
    compilerArgs << '-Xlint:-options'
    compilerArgs << '-Werror'
    encoding = 'utf-8'
  }
}

tasks.withType(Test) {
  // Turn on logging for all tests, filter to show failures/skips only
  testLogging {
    exceptionFormat 'full'
    showCauses true
    showExceptions true
    showStackTraces true
    events 'failed', 'skipped'
  }
}

tasks.withType(Javadoc) {
  title = "${project.name} ${project.version}"
  configure(options) {
    header = project.name
    encoding 'UTF-8'
    docEncoding 'UTF-8'
    charSet 'UTF-8'
    linkSource true
    author = true
    links('http://docs.oracle.com/javase/8/docs/api/',
      'http://docs.oracle.com/javaee/7/api/')
    exclude "**/*Test.java"
    if (JavaVersion.current().java8Compatible) addStringOption('Xdoclint:none', '-quiet')
  }
}
