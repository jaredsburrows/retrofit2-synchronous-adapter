plugins {
  id "com.gradle.build-scan" version "1.16"
  id "com.jfrog.artifactory" version "4.7.5"
  id "com.jfrog.bintray" version "1.8.4"
  id "com.github.ben-manes.versions" version "0.20.0"
  id "net.ltgt.errorprone" version "0.6"
  id "java-library"
  id "maven-publish"
}

repositories {
  jcenter()
}

apply from: rootProject.file("gradle/dependencies.gradle")

group = GROUP
version = VERSION_NAME
description = POM_DESCRIPTION

sourceCompatibility = ext.javaVersion
targetCompatibility = ext.javaVersion

jar {
  manifest {
    attributes(
      "Implementation-Title": POM_NAME,
      "Implementation-Version": version,
      "Built-By": System.getProperty("user.name"),
      "Built-Date": new Date(),
      "Built-JDK": System.getProperty("java.version"),
      "Built-Gradle": gradle.gradleVersion)
  }
}

dependencies {
  implementation deps.retrofit

  compileOnly deps.jsr305

  testImplementation deps.junit
  testImplementation deps.assertjCore
  testImplementation deps.mockitoInline
  testImplementation deps.mockwebserver
  testImplementation deps.converterGson
  testImplementation deps.jsr305
}

apply from: "gradle/scan.gradle"
apply from: "gradle/compile.gradle"
apply from: "gradle/publish.gradle"
